apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'clickstack'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

group = 'com.cloudbees.genapp'
clickstackName = 'tomcat8-clickstack'
version = '1.0.0-SNAPSHOT'

def tomcatVersion = "8.0.0-RC1"
mainClassName = 'com.cloudbees.genapp.tomcat.Setup'

repositories {
    mavenCentral()
    maven {
        // as documented on https://code.google.com/p/spymemcached/wiki/Maven
        url "http://files.couchbase.com/maven2/"
    }
}

configurations {
    clickstackPackage.ext.clickstackFolder = ""

    clickstackRuntimeJavaAgent.ext.clickstackFolder = "deps/javaagent-lib"

    clickstackRuntime.ext.clickstackFolder = "deps/tomcat-lib"
    clickstackRuntimePostgresql.ext.clickstackFolder = "deps/tomcat-lib-postgresql"
    clickstackRuntimeMySql.ext.clickstackFolder = "deps/tomcat-lib-mysql"
    clickstackRuntimeMail.ext.clickstackFolder = "deps/tomcat-lib-mail"
    clickstackRuntimeMemcache.ext.clickstackFolder = "deps/tomcat-lib-memcache"

    clickstackControl.ext.clickstackFolder = "deps/control-lib"
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-core:2.1.3'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.1.3'
    compile 'com.google.code.findbugs:jsr305:2.0.1'
    compile 'com.google.guava:guava:14.0.1'
    compile 'org.slf4j:slf4j-api:1.7.5'
    compile 'org.slf4j:slf4j-simple:1.7.5'

    testCompile 'junit:junit:4.10'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.xmlmatchers:xml-matchers:1.0-RC1'

    clickstackPackage "org.apache.tomcat:tomcat:$tomcatVersion@zip"
    clickstackControl 'com.cloudbees:cloudbees-jmx-invoker:1.0.2:jar-with-dependencies'
    clickstackRuntimeJavaAgent 'org.jmxtrans.agent:jmxtrans-agent:1.0.6'
    clickstackRuntimeJavaAgent 'com.cloudbees.clickstack:cloudbees-clickstack-javaagent:1.2.0'
    clickstackRuntime 'com.cloudbees:cloudbees-web-container-extras:1.0.1'
    clickstackRuntimePostgresql 'postgresql:postgresql:9.1-901-1.jdbc4'
    clickstackRuntimeMySql 'mysql:mysql-connector-java:5.1.25'
    clickstackRuntimeMail 'javax.mail:mail:1.4.7'
    clickstackRuntimeMemcache('de.javakaffee.msm:memcached-session-manager-tc7:1.6.4') {
        exclude group: 'couchbase', module: 'couchbase-client'
    }
    clickstackRuntimeMemcache 'de.javakaffee.msm:msm-kryo-serializer:1.6.4'
}

// TODO move this in the clickstack-gradle-plugin
configurations.each { cfg ->
    if (cfg.ext.has("clickstackFolder")) {
        clickstackDistribution.from(cfg.collect { it }) {
            into "$cfg.ext.clickstackFolder"
        }
    } else {
        logger.debug "Ignore $cfg.name"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.7'
}


idea {
    project {
        languageLevel = '1.7'
    }
}
